buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    //classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
  compileSdkVersion buildConfig.compileSdk

  defaultConfig {
    applicationId "com.ouattararomuald.saviezvousque"
    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk
    versionCode 18
    versionName '3.2.2'

    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    vectorDrawables.useSupportLibrary true

    resConfigs 'fr'
  }

  packagingOptions {
    exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  buildFeatures {
    dataBinding = true
  }

  lintOptions {
    textReport true
    textOutput 'stdout'

    // InvalidPackage: Needed cause of SimpleXml
    disable 'AllowBackup', 'InvalidPackage', 'GoogleAppIndexingWarning' // We don't care
  }

  buildTypes {
    def DATABASE_NAME_KEY = 'DATABASE_NAME'
    def FEED_BASE_URL = '"http://saviezvousque.net/wp-json/wp/v2/"'
    def FEED_BASE_URL_KEY = 'FEED_BASE_URL'

    debug {
      buildConfigField 'String', DATABASE_NAME_KEY, '"svqDbDebug"'
      buildConfigField 'String', FEED_BASE_URL_KEY, FEED_BASE_URL

      applicationIdSuffix '.debug'
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField 'String', DATABASE_NAME_KEY, '"HaveFun"'
      buildConfigField 'String', FEED_BASE_URL_KEY, FEED_BASE_URL
    }
  }

  androidExtensions {
    experimental = true
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation deps.kotlin.stdlib.jdk8
  implementation project(':db')
  implementation project(':downloaders')

  implementation deps.coroutines.core

  implementation deps.dagger.runtime
  kapt deps.dagger.compiler

  implementation deps.androidx.appcompat
  implementation deps.androidx.constraintlayout
  implementation deps.androidx.legacy
  implementation deps.androidx.material
  implementation deps.androidx.recyclerview
  implementation deps.androidx.annotation
  implementation deps.androidx.vectordrawable
  implementation deps.androidx.arch.core.ktx

  implementation deps.androidx.databinding.library
  kapt deps.androidx.databinding.compiler

  implementation deps.androidx.arch.lifecycle.extensions
  implementation deps.androidx.arch.lifecycle.java8
  implementation deps.androidx.arch.lifecycle.reactivestreams

  implementation deps.groupie.runtime
  implementation deps.groupie.extensions
  implementation deps.groupie.databinding

  implementation deps.picasso

  implementation deps.stetho

  implementation deps.rxAndroid
  implementation deps.androidx.arch.room.rxjava2
  implementation deps.androidx.arch.room.runtime
  kapt deps.androidx.arch.room.compiler

  implementation deps.retrofit.rxjavaCallAdapter
  implementation deps.retrofit.gsonConverter

  implementation deps.threetenabp

  testImplementation deps.junit
  testImplementation deps.coroutines.test
  testImplementation deps.robolectric
  testImplementation deps.truth

  androidTestImplementation deps.junit
  androidTestImplementation deps.androidx.test.ext.junit
  androidTestImplementation deps.androidx.test.ext.junitKtx
  androidTestImplementation deps.androidx.test.espresso.runner
  androidTestImplementation deps.androidx.test.espresso.core
  androidTestImplementation deps.androidx.paging.testing
  androidTestImplementation deps.mockito
  androidTestImplementation deps.truth
}

//apply plugin: 'com.getkeepsafe.dexcount'

repositories {
  mavenCentral()
}
