apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.squareup.sqldelight'

android {
  compileSdkVersion buildConfig.compileSdk

  defaultConfig {
    minSdkVersion buildConfig.minSdk

    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    resConfigs 'fr'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
          "room.schemaLocation": "$projectDir/schemas".toString(),
          "room.incremental": "true",
          "room.expandProjection": "true"
        ]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11.toString()
  }

  lintOptions {
    textReport true
    textOutput 'stdout'

    // InvalidPackage: Needed cause of SimpleXml
    disable 'InvalidPackage'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation deps.kotlin.stdlib.jdk8
  api project(':common')

  api deps.dagger.runtime
  kapt deps.dagger.compiler

  api deps.sqlDelight.androidDriver
  api deps.sqlDelight.coroutines

  api deps.androidx.paging.runtime
  api deps.androidx.paging.rxjava2

  api deps.threetenabp

  testImplementation deps.junit
  testImplementation deps.mockito
  testImplementation deps.retrofit.mock
  testImplementation deps.robolectric
  testImplementation deps.sqlDelight.jdbcDriver
  testImplementation deps.jvmThreetenabp
  testImplementation deps.truth

  androidTestImplementation deps.junit
  androidTestImplementation deps.truth
  androidTestImplementation deps.androidx.arch.coreTesting
  androidTestImplementation deps.androidx.test.ext.junit
  androidTestImplementation deps.androidx.test.espresso.runner
  androidTestImplementation deps.androidx.test.espresso.core
}
