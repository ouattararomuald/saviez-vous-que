// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.buildConfig = [
    'compileSdk': 28,
    'minSdk': 18,
    'targetSdk' : 28,
  ]

  ext.versions = [
    'androidPlugin': '3.3.0-alpha13',
    'arch': '1.1.1',
    'dagger': '2.17',
    'kotlin': '1.2.71',
    'retrofit': '2.4.0',
    'room': '1.1.1',
    'supportLibray': '28.0.0',
  ]

  ext.deps = [
    'android': [
      'arch': [
        'lifecycle': [
          'extensions': "android.arch.lifecycle:extensions:${versions.arch}",
          'viewModel': "android.arch.lifecycle:viewmodel:${versions.arch}",
        ],
        'room': [
          'rxjava': "android.arch.persistence.room:rxjava2:${versions.room}",
          'runtime': "android.arch.persistence.room:runtime:${versions.room}",
          'compiler': "android.arch.persistence.room:compiler:${versions.room}",
          'testHelper': "android.arch.persistence.room:testing:${versions.room}"
        ],
        'coreTesting': 'android.arch.core:core-testing:1.1.1'
      ],
      'kotlinExtensions': 'androidx.core:core-ktx:0.1',
      'support': [
        'appcompatV7': "com.android.support:appcompat-v7:${versions.supportLibray}",
        'constraintLayout': 'com.android.support.constraint:constraint-layout:2.0.0-alpha2',
        'design': "com.android.support:design:${versions.supportLibray}",
        'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibray}",
        'supportAnnotations': "com.android.support:support-annotations:${versions.supportLibray}",
        'vectorDrawables': "com.android.support:support-vector-drawable:${versions.supportLibray}",
        'databinding': [
          'compiler': 'com.android.databinding:compiler:3.2.0-alpha10',
          'library': 'com.android.databinding:library:3.3.0-alpha01',
        ],
      ],
      'test': [
        'espresso': [
          'runner': 'com.android.support.test:runner:1.0.2',
          'core': 'com.android.support.test.espresso:espresso-core:3.0.2',
        ],
      ],
    ],
    'dagger': [
      'runtime': "com.google.dagger:dagger:${versions.dagger}",
      'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'junit': 'junit:junit:4.12',
    'retrofit': [
      'mock': "com.squareup.retrofit2:retrofit-mock:${versions.retrofit}",
      'rxjavaCallAdapter': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
      'gsonConverter': "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    ],
    'fresco': 'com.facebook.fresco:fresco:1.10.0',
    'gson': 'com.google.code.gson:gson:2.8.5',
    'rxAndroid': 'io.reactivex.rxjava2:rxandroid:2.1.0',
    'stetho': 'com.facebook.stetho:stetho:1.5.0',
    'threetenabp': 'com.jakewharton.threetenabp:threetenabp:1.1.1',
    'truth': 'com.google.truth:truth:0.42',
  ]

  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${versions.androidPlugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC9.2'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
}

subprojects { project ->

  repositories {
    google()
    jcenter()
  }

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  toolVersion = "1.0.0.RC9.2"
  input = files("$projectDir/app/src/main/java")
  config = files("$projectDir/code-analysis/detekt.yml")
  filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}
