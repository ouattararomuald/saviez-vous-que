// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '1.2.30'
  ext.buildConfig = [
    'compileSdk': 27,
    'minSdk': 17,
    'targetSdk' : 27,
  ]

  ext.versions = [
    'androidPlugin': '3.2.0-alpha04',
    'arch': '1.1.0',
    'arch_room': '1.1.0-alpha2',
    'dagger': '2.14.1',
    'databinding': '3.2.0-alpha04',
    'kotlin': '1.2.30',
    'retrofit': '2.3.0',
    'supportLibray': '27.0.2',
  ]

  ext.deps = [
    'android': [
      'arch': [
        'lifecycle': [
          'extensions': "android.arch.lifecycle:extensions:${versions.arch}",
          'viewModel': "android.arch.lifecycle:viewmodel:${versions.arch}",
        ],
        'room': [
          'rxjava': "android.arch.persistence.room:rxjava2:${versions.arch_room}",
          'runtime': "android.arch.persistence.room:runtime:${versions.arch_room}",
          'compiler': "android.arch.persistence.room:compiler:${versions.arch_room}",
          'testHelper': "android.arch.persistence.room:testing:${versions.arch_room}"
        ],
        'coreTesting': 'android.arch.core:core-testing:1.1.0'
      ],
      'kotlinExtensions': 'androidx.core:core-ktx:0.1',
      'support': [
        'appcompatV7': "com.android.support:appcompat-v7:${versions.supportLibray}",
        'constraintLayout': "com.android.support.constraint:constraint-layout:1.1.0-beta5",
        'design': "com.android.support:design:${versions.supportLibray}",
        'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibray}",
        'supportAnnotations': "com.android.support:support-annotations:${versions.supportLibray}",
        'vectorDrawables': "com.android.support:support-vector-drawable:${versions.supportLibray}",
        'databinding': [
          'compiler': "com.android.databinding:compiler:${versions.databinding}",
          'library': "com.android.databinding:library:${versions.databinding}",
        ],
      ],
      'test': [
        'espresso': [
          'runner': 'com.android.support.test:runner:1.0.1',
          'core': 'com.android.support.test.espresso:espresso-core:3.0.1',
        ],
      ],
    ],
    'dagger': [
      'runtime': "com.google.dagger:dagger:${versions.dagger}",
      'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'junit': 'junit:junit:4.12',
    'retrofit': [
      'mock': "com.squareup.retrofit2:retrofit-mock:${versions.retrofit}",
      'rxjavaCallAdapter': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
      'gsonConverter': "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    ],
    'fresco': 'com.facebook.fresco:fresco:1.8.1',
    'gson': 'com.google.code.gson:gson:2.8.2',
    'materialDrawer': 'com.mikepenz:materialdrawer:6.0.6@aar',
    'rxAndroid': 'io.reactivex.rxjava2:rxandroid:2.0.2',
    'threetenabp': 'com.jakewharton.threetenabp:threetenabp:1.0.5',
    'truth': 'com.google.truth:truth:0.39',
  ]

  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${versions.androidPlugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC6-3'
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
}

subprojects { project ->

  repositories {
    google()
    jcenter()
  }

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  version = "1.0.0.RC6-3"
  profile("main") {
    input = "$projectDir/app/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
}
