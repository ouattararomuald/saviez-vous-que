// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.buildConfig = [
    'compileSdk': 27,
    'minSdk': 17,
    'targetSdk' : 27,
  ]

  ext.versions = [
    'androidPlugin': '3.2.0-alpha02',
    'arch': '1.1.0-alpha1',
    'dagger': '2.14.1',
    'kotlin': '1.2.21',
    'retrofit': '2.3.0',
    'supportLibray': '27.0.2',
  ]

  ext.deps = [
    'android': [
      'arch': [
        'room': [
          'rxjava': "android.arch.persistence.room:rxjava2:${versions.arch}",
          'runtime': "android.arch.persistence.room:runtime:${versions.arch}",
          'compiler': "android.arch.persistence.room:compiler:${versions.arch}",
        ],
      ],
      'kotlinExtensions': 'androidx.core:core-ktx:0.1',
      'support': [
        'appcompatV7': "com.android.support:appcompat-v7:${versions.supportLibray}",
      ],
      'test': [
        'espresso': [
          'runner': 'com.android.support.test:runner:1.0.1',
          'core': 'com.android.support.test.espresso:espresso-core:3.0.1',
        ],
      ],
    ],
    'dagger': [
      'runtime': "com.google.dagger:dagger:${versions.dagger}",
      'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'mockWebserver': 'com.squareup.okhttp3:mockwebserver:3.9.1',
    'junit': 'junit:junit:4.12',
    'retrofit': [
      'mock': "com.squareup.retrofit2:retrofit-mock:${versions.retrofit}",
      'rxjavaCallAdapter': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
      'xmlConverter': "com.squareup.retrofit2:converter-simplexml:${versions.retrofit}"
    ],
    'truth': 'com.google.truth:truth:0.39',
  ]

  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${versions.androidPlugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC6-3'
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
}

subprojects { project ->

  repositories {
    google()
    jcenter()
  }

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  version = "1.0.0.RC6-3"
  profile("main") {
    input = "$projectDir/app/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
}
